;****************************	;SWOS installer by Codetapper/Action 18/02/2001

(set #sub-dir "data")		;sub directory containing data files
(set #readme-file "ReadMe")	;name of readme file
(set #cleanup "")		;files to delete after install

;****************************
;----------------------------
; checks if given program is installed, if not abort install
; #program - to check

(procedure P_ChkRun
  (if
    (= 0 (run ("cd SYS:\nWhich %s" #program)))
    ("")
    (abort
      (cat
	"You must install \"" #program "\" first!\n"
	"It must be accessible via the path.\n"
	"You can find it in the WHDLoad package."
      )
    )
  )
)

;----------------------------
; Wait for inserting disk
; IN:  #AD_disk - name of disk
; OUT: -

(procedure P_disk
  (askdisk
    (dest #AD_disk)
    (prompt ("\nInsert Disk \"%s\" in any drive!" #AD_disk))
    (help @askdisk-help)
  )
)

;****************************

(if
  (exists #readme-file)
  (if
    (= 0 (run ("SYS:Utilities/Multiview %s" #readme-file)))
    ("")
    (run ("SYS:Utilities/More %s" #readme-file))
  )
)

(set #program "WHDLoad")
(P_ChkRun)

(set @default-dest
  (askdir
    (prompt ("Where should \"%s\" be installed?\nA drawer \"%s\" will be automatically created." @app-name @app-name))
    (help @askdir-help)
    (default @default-dest)
    (disk)
  )
)
(set #dest (tackon @default-dest @app-name))
(if
  (exists #dest)
  (
    (set #choice
      (askbool
        (prompt ("\nDirectory \"%s\" already exists.\n Should it be deleted?" #dest))
        (default 1)
        (choices "Delete" "Skip")
        (help @askbool-help)
      )
    )
    (if
      (= #choice 1)
      (run ("Delete \"%s\" \"%s.info\" All" #dest #dest))
    )
  )
)
(makedir #dest
  (help @makedir-help)
  (infos)
)

;----------------------------

(copyfiles
  (help @copyfiles-help)
  (source ("%s.Slave" @app-name))
  (dest #dest)
)
(if
  (exists ("%s.newicon" @app-name))
  (set #icon
    (askchoice
      (prompt "\nWhich icon would you like to install?\nAll these great icons were created by Frank!")
      (default 0)
      (choices "Normal" "NewIcon" "NewIcon (no fade - fewer colours)" "GlowIcon" "GlowIcon (no fade - fewer colours)")
      (help @askchoice-help)
    )
  )
  (set #icon 0)
)
(select #icon
  (set #icon ("%s.inf" @app-name))
  (set #icon ("%s.newicon" @app-name))
  (set #icon ("%s.newicon2" @app-name))
  (set #icon ("%s.glowicon" @app-name))
  (set #icon ("%s.glowicon2" @app-name))
)
(copyfiles
  (help @copyfiles-help)
  (source #icon)
  (newname ("%s.info" @app-name))
  (dest #dest)
)
(tooltype
  (dest (tackon #dest @app-name))
  (settooltype "Slave" ("%s.Slave" @app-name))
  (setdefaulttool "WHDLoad")
  (setstack 10240)
  (noposition)
)
(if
  (exists #readme-file)
  (copyfiles
    (help @copyfiles-help)
    (source #readme-file)
    (dest #dest)
  )
)
(if
  (exists ("%s.info" #readme-file))
  (
    (copyfiles
      (help @copyfiles-help)
      (source ("%s.info" #readme-file))
      (dest #dest)
    )
    (tooltype
      (dest (tackon #dest #readme-file))
      (noposition)
    )
  )
)
(if
  (= #sub-dir "")
  ("")
  (
    (set #dest (tackon #dest #sub-dir))
    (makedir #dest
      (help @makedir-help)
    )
  )
)

(makedir (tackon #dest "save")
  (help @makedir-help)
)
(copyfiles
  (help @copyfiles-help)
  (source "swos11.dku")
  (dest #dest)
)

;----------------------------

(set #AD_disk "SWOS")
(P_disk)
(copyfiles
  (help @copyfiles-help)
  (source ("%s:" #AD_disk))
  (dest #dest)
  (pattern "~(s|SSBOOT|SWOS|SWOS.REL)")
)

(set #AD_disk "SWOS2")
(P_disk)
(copyfiles
  (help @copyfiles-help)
  (source ("%s:" #AD_disk))
  (dest #dest)
  (all)
)

;----------------------------

(run ("Delete %s ALL QUIET FORCE" #cleanup))

(exit)
