;****************************

(set #readme-file "README") ;name of readme file
(set #last-disk 1)          ;amount of disks
(set #disk-size 901120)     ;size of each disk
(set #sub-dir "")

;****************************
;----------------------------
; open drawer if opendrawer command is available
; #dest - path

(procedure P_OpenDrawer
  (if
    (= 0 (run "cd SYS:\nWhich opendrawer"))
    (run ("opendrawer %s" #dest-org))
    ("")
  )
)

;****************************
;----------------------------
; Checks if given program is reachable via the path
; if not abort install
; IN:  #program - to check
; OUT: -

(procedure P_chkrun
  (if
    (= 0 (run ("cd SYS:\nWhich %s" #program)))
    ("")
    (abort ("You must install \"%s\" first !\nIt must be accessible via the path.\nYou can find it in the whdload package." #program))
  )
)
;----------------------------
; Wait for inserting disk
; IN:  #AD_disk - name of disk
; OUT: -

(procedure P_disk
  (askdisk
    (dest #AD_disk)
    (prompt ("\nInsert Disk \"%s\" in any drive!" #AD_disk))
    (help @askdisk-help)
  )
)
;----------------------------
; Create disk-Image using DIC
; IN:  #dest        - DestinationPath
;      #CI_diskname - DiskName
;      #CI_diskno   - DiskNumber
;      #CI_drive    - DriveToReadFrom
; OUT: -

(procedure P_image
  (message ("\nInsert \"%s\" into drive %s !\n\n(make sure it's the right disk because it will not checked)" #CI_diskname #CI_drive))
  (if
    (= 0 
      (run ("cd \"%s\"\nDIC %s FD=%ld LD=%ld SIZE=%ld >CON:///1000//CLOSE" #dest #CI_drive #CI_diskno #CI_diskno #disk-size))
    )
    ("")
    (abort "\"DIC\" has failed to create a diskimage")
  )
)

;****************************

(if
  (< @installer-version (+ (* 44 65536) 10))
  (
  (message
    (cat
      "Warning: your installer is outdated.\n"
      "All features of this installation won't be available, such as icon show and drawer opening.\n"
      "You have version " (/ @installer-version 65536) "." 
      (BITAND @installer-version 65535) ", needed is at least version 44.10. "
      "The 'installer' 44.10 comes with OS 3.5 but is also contained in the NDK 3.9. "
      "You may also use the InstallerNG by Jens TrÃ¶ger."
      "\n\n"
      "The installers can be obtained from the net:\n"
      "http://www.amiga.com/3.9/download/NDK3.9.lha\n"
      "aminet:util/sys/InstallerNG.lha"
    )
  )
  (set #newstuff 0)
  )
  (set #newstuff 1)
)


(if
  (exists #readme-file)
  (if
    (= 0 (run ("SYS:Utilities/Multiview %s" #readme-file)))
    ("")
    (run ("SYS:Utilities/More %s" #readme-file))
  )
)

(set #program "WHDLoad")
(P_chkrun)

  (set #version
    (askchoice
      (prompt "\nWhich version do you want to install ?\n")
      (default 0)
      (choices "floppy"
			   "CDTV"
      )
      (help @askchoice-help)
   )
  )
(set @default-dest
  (askdir
    (prompt ("Where should \"%s\" installed ?\nA drawer \"%s\" will automatically created." @app-name @app-name))
    (help @askdir-help)
    (default @default-dest)
    (disk)
  )
)
(set #dest (tackon @default-dest @app-name))
(if
  (exists #dest)
  (
    (set #choice
      (askbool
        (prompt ("\nDirectory \"%s\" already exists.\n Should it be deleted ?" #dest))
        (default 1)
        (choices "Delete" "Skip")
        (help @askbool-help)
      )
    )
    (if
      (= #choice 1)
      (run ("Delete \"%s\" \"%s.info\" All" #dest #dest))
    )
  )
)
(makedir #dest
  (help @makedir-help)
  (infos)
)
  
(if (= 0 #version)
(
(set #slave_name "Prehistorik.slave")
(set #program "DIC")
(P_chkrun)

; in expert mode ask for source drive
(if
  (= @user-level 2)
  (
    (set #CI_drive
      (askchoice
	(prompt "Select source drive for diskimages")
	(default 0)
	(choices "DF0:" "DF1:" "RAD:" "Enter Device")
	(help @askchoice-help)
      )
    )
    (select #CI_drive
      (set #CI_drive "DF0:")
      (set #CI_drive "DF1:")
      (set #CI_drive "RAD:")
      (set #CI_drive
        (askstring
          (prompt "Select source drive for diskimages")
          (default "DF0:")
          (help @askstring-help)
        )
      )
    )
  )
  (set #CI_drive "DF0:")
)
)
(
(set #slave_name "PrehistorikCDTV.slave")
(set #sub-dir "data")
)
)


;----------------------------

(copyfiles
  (help @copyfiles-help)
  (source #slave_name)
  (dest #dest)
  (newname "Prehistorik.slave")
)
  (set #icon
    (askchoice
      (prompt "\nWhich icon (all by Frank) do you like to install ?\n")
      (default 0)
      (choices "Newicon 1" "NewIcon 2" "Glowicon 1" "Glowicon 2")
      (help @askchoice-help)
    )
  )

(select #icon
  (set #icon ("icons/%s_a_NI_xtd.info" @app-name))
  (set #icon ("icons/%s_b_NI_xtd.info" @app-name))
  (set #icon ("icons/%s_a_GL.info" @app-name))
  (set #icon ("icons/%s_b_GL.info" @app-name))
)
(copyfiles
  (help @copyfiles-help)
  (source #icon)
  (newname ("%s.info" @app-name))
  (dest #dest)
)
(if
  (exists #readme-file)
  (copyfiles
    (help @copyfiles-help)
    (source #readme-file)
    (dest #dest)
  )
)
(if
  (exists ("%s.info" #readme-file))
  (copyfiles
    (help @copyfiles-help)
    (source ("%s.info" #readme-file))
    (dest #dest)
  )
)
(set #dest-org #dest)
(if
  (= #sub-dir "")
  ("")
  (
    (set #dest (tackon #dest #sub-dir))
    (makedir #dest
      (help @makedir-help)
    )
  )
)

(if (= 0 #version)
(
(set #AD_disk "MICKEY")
(set #CI_diskno 1)
(while
  (<= #CI_diskno #last-disk)
  (
    (set #CI_diskname ("%s Disk %ld" @app-name #CI_diskno))
    (P_image)
    (run ("FileNote Disk.%ld %s Quiet" #CI_diskno @app-name))
    (set #CI_diskno (+ #CI_diskno 1))
  )
)
)
(
(set #AD_disk "Prehistorik")
(P_disk)
      (copyfiles
        (help @copyfiles-help)
        (source ("%s:" #AD_disk))
        (dest #dest)
        (all)
      )

)
)

(if (= #newstuff 1)
  (openwbobject #dest-org)
)
;----------------------------

(exit)

