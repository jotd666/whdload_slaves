;****************************

(set #sub-dir "data")		;sub directory containing data files
(set #readme-file "readme")	;name of readme file
(set #gamedir "Pirates")

;****************************

;----------------------------
; Checks if given program is reachable via the path
; if not abort install
; IN:  #program - to check
; OUT: -

(procedure P_chkrun
  (if
    (= 0 (run ("cd SYS:\nWhich %s" #program)))
    ("")
    (abort ("You must install \"%s\" first !\nIt must be accessible via the path.\nYou can find it in the whdload package." #program))
  )
)


;----------------------------
; Wait for inserting disk
; IN:  #AD_disk - name of disk
; OUT: -

(procedure P_disk
  (askdisk
    (dest #AD_disk)
    (prompt ("\nInsert Disk \"%s\" in any drive !" #AD_disk))
    (help @askdisk-help)
  )
)


(if
  (< @installer-version (+ (* 44 65536) 10))
  (
  (message
    (cat
      "Warning: your installer is outdated.\n"
      "All features of this installation won't be available, such as icon show and drawer opening.\n"
      "You have version " (/ @installer-version 65536) "." 
      (BITAND @installer-version 65535) ", needed is at least version 44.10. "
      "The 'installer' 44.10 comes with OS 3.5 but is also contained in the NDK 3.9. "
      "You may also use the InstallerNG by Jens Tröger."
      "\n\n"
      "The installers can be obtained from the net:\n"
      "http://www.amiga.com/3.9/download/NDK3.9.lha\n"
      "aminet:util/sys/InstallerNG.lha"
    )
  )
  (set #newstuff 0)
  )
  (set #newstuff 1)
)

;****************************

(if
  (exists #readme-file)
  (if 
    (= 0 (run ("SYS:Utilities/Multiview %s" #readme-file)))
    ("")
    (run ("SYS:Utilities/More %s" #readme-file))
  )
)

(set #program "WHDLoad")
(P_chkrun)


(if
  (getenv "WHDLInstPath")
  (set @default-dest (getenv "WHDLInstPath"))
)

(set #dest
  (askdir
    (prompt ("Where should \"%s\" installed ?\nA drawer \"%s\" will automatically created." @app-name #gamedir))
    (help @askdir-help)
    (default @default-dest)
    (disk)
  )
)

; update for the end message
(set @default-dest #dest)


(run ("setenv WHDLInstPath \"%s\"\ncopy ENV:WHDLInstPath ENVARC:" #dest))

(set #dest (tackon @default-dest #gamedir))
(if
  (exists #dest)
  (
    (set #choice
      (askbool
        (prompt ("\nDirectory \"%s\" already exists.\n Should it be deleted ?" #dest))
        (default 1)
        (choices "Delete" "Skip")
        (help @askbool-help)
      )
    )
    (if
      (= #choice 1)
      (run ("Delete \"%s\" \"%s.info\" All" #dest #dest))
    )
  )
)
(makedir #dest
  (help @makedir-help)
  (infos)
)

(set #version
    (askchoice
      (prompt ("Which version of %s do you like to install?" @app-name))
      (default 0)
      (choices "Floppy ECS version" "Floppy AGA (unofficial) version" "CD³² version")
      (help @askchoice-help)
    )
  )

(select #version
  (set #suffix "ECS")
  (set #suffix "CD32")
  (set #suffix "CD32")
)
;----------------------------

(copyfiles
  (help @copyfiles-help)
  (source ("%s%s.slave" #gamedir #suffix))
  (dest #dest)
  (newname ("%s.slave" #gamedir))
)

(set #icon ("%s.exoticon" #gamedir))

(copyfiles
  (help @copyfiles-help)
  (source #icon)
  (newname ("%s.info" #gamedir))
  (dest #dest)
)
(if
  (exists #readme-file)
  (copyfiles
    (help @copyfiles-help)
    (source #readme-file)
    (dest #dest)
  )
)
(if
  (exists ("%s.info" #readme-file))
  (copyfiles
    (help @copyfiles-help)
    (source ("%s.info" #readme-file))
    (dest #dest)
  )
)
(if
  (= #sub-dir "")
  ("")
  (
    (set #datadest (tackon #dest #sub-dir))
    (makedir #datadest
      (help @makedir-help)
    )
  )
)

(select #version
(

;----------------------------
;            ECS
;----------------------------

      (set #AD_disk "PiratesA")
      (P_disk)
      (copyfiles
        (help @copyfiles-help)
        (source ("%s:" #AD_disk))
        (dest #datadest)
        (pattern "~(c|s|l|Pirates#?Install#?|#?.info)")
      )
      (set #AD_disk "PiratesB")
      (P_disk)
      (copyfiles
        (help @copyfiles-help)
        (source ("%s:" #AD_disk))
        (dest #datadest)
        (pattern "~(#?.info)")
      )

      ; copy the missing system-configuration file

      (copyfiles
        (help @copyfiles-help)
        (source "devs")
        (dest #datadest)
	(all)
      )


)

;----------------------------
;        AGA (CD32 hacked)
;----------------------------

(
; for savegames

  (makedir (tackon #datadest "Pirates.NVD")
   (help @makedir-help)
  )

      (set #AD_disk "piratesg")
      (P_disk)
      (copyfiles
        (help @copyfiles-help)
        (source ("%s:" #AD_disk))
        (dest #datadest)
        (pattern "~(c|s|l|Pirates#?Install#?|#?.info)")
      )
      (set #AD_disk "pg2")
      (P_disk)
      (copyfiles
        (help @copyfiles-help)
        (source ("%s:" #AD_disk))
        (dest #datadest)
        (pattern "~(#?.info)")
      )


)
;----------------------------
;            CD32
;----------------------------

(
; for savegames

  (makedir (tackon #datadest "Pirates.NVD")
   (help @makedir-help)
  )

(set #AD_disk "PIRATES_GOLD")
(P_disk)

(copyfiles
  (help @copyfiles-help)
  (source ("%s:pirates" #AD_disk))
  (dest #datadest)
  (all)
)
(copyfiles
  (help @copyfiles-help)
  (source ("%s:pirates.font" #AD_disk))
  (dest #datadest)
  (all)
)
(copyfiles
  (help @copyfiles-help)
  (source ("%s:cd32rez" #AD_disk))
  (dest #datadest)
  (all)
)
(copyfiles
  (help @copyfiles-help)
  (source ("%s:pirates_german" #AD_disk))
  (dest #datadest)
  (all)
)
)
)

(if (= #newstuff 1)
   (openwbobject #dest)
)

(exit)
