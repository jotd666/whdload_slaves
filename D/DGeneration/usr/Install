;****************************

(set #sub-dir "data")		;sub directory containing data files
(set #readme-file "readme")	;name of readme file
(set #gamedir "DGeneration")

;****************************

;----------------------------
; Checks if given program is reachable via the path
; if not abort install
; IN:  #program - to check
; OUT: -

(procedure P_chkrun
  (if
    (= 0 (run ("cd SYS:\nWhich %s" #program)))
    ("")
    (abort ("You must install \"%s\" first !\nIt must be accessible via the path.\nYou can find it in the whdload package." #program))
  )
)

;****************************
;----------------------------
; open drawer if opendrawer command is available
; #dest - path

(procedure P_OpenDrawer
  (if
    (= 0 (run "cd SYS:\nWhich opendrawer"))
    (run ("opendrawer %s" #dest))
    ("")
  )
)

;----------------------------
; Wait for inserting disk
; IN:  #AD_disk - name of disk
; OUT: -

(procedure P_disk
  (askdisk
    (dest #AD_disk)
    (prompt ("\nInsert Disk \"%s\" in any drive !" #AD_disk))
    (help @askdisk-help)
  )
)


;****************************

(if
  (exists #readme-file)
  (if 
    (= 0 (run ("SYS:Utilities/Multiview %s" #readme-file)))
    ("")
    (run ("SYS:Utilities/More %s" #readme-file))
  )
)

(set #program "WHDLoad")
(P_chkrun)

(set default-dest
  (askdir
    (prompt ("Where should \"%s\" installed ?\nA drawer \"%s\" will automatically created." @app-name #gamedir))
    (help @askdir-help)
    (default @default-dest)
    (disk)
  )
)
(set @default-dest default-dest)
(set #dest (tackon @default-dest #gamedir))
(if
  (exists #dest)
  (
    (set #choice
      (askbool
        (prompt ("\nDirectory \"%s\" already exists.\n Should it be deleted ?" #dest))
        (default 1)
        (choices "Delete" "Skip")
        (help @askbool-help)
      )
    )
    (if
      (= #choice 1)
      (run ("Delete \"%s\" \"%s.info\" All" #dest #dest))
    )
  )
)
(makedir #dest
  (help @makedir-help)
  (infos)
)


(set #game_ver
	(askchoice
		(prompt "Which version of D/Generation\ndo you wish to install ?")
		(help	@askoptions-help)
		(choices "CD32 version" "ECS Floppy version" "AGA Floppy version")
	)
)

(select #game_ver
  (set #srcslave ("%sCD32.slave" #gamedir))
  (set #srcslave ("%sECS.slave" #gamedir))
  (set #srcslave ("%sAGA.slave" #gamedir))
)

;----------------------------

(copyfiles
  (help @copyfiles-help)
  (source #srcslave)
  (dest #dest)
  (newname (tackon #dest ("%s.slave" #gamedir)))
)

(if
  (exists ("%s.exoticon" #gamedir))
  (set #icon
    (askchoice
      (prompt "\nWhich icon do you like to install ?\n")
      (default 0)
      (choices "NewIcon" "Exotic NewIcon" "Exoticon")
      (help @askchoice-help)
    )
  )
  (set #icon 0)
)
(select #icon
  (set #icon ("%s.newicon" #gamedir))
  (set #icon ("%s.newexot" #gamedir))
  (set #icon ("%s.exoticon" #gamedir))
)
(copyfiles
  (help @copyfiles-help)
  (source #icon)
  (newname ("%s.info" #gamedir))
  (dest #dest)
)
(if
  (exists #readme-file)
  (copyfiles
    (help @copyfiles-help)
    (source #readme-file)
    (dest #dest)
  )
)
(if
  (exists ("%s.info" #readme-file))
  (copyfiles
    (help @copyfiles-help)
    (source ("%s.info" #readme-file))
    (dest #dest)
  )
)
(if
  (= #sub-dir "")
  ("")
  (
    (set #datadest (tackon #dest #sub-dir))
    (makedir #datadest
      (help @makedir-help)
    )
  )
)

(if (= 0 #game_ver)
(

; copy a dummy savefile else the save operation flashes like crazy
; and the os swap make the key stick !!
(copyfiles
  (help @copyfiles-help)
  (source "nvram")
  (dest #datadest)
)

(set #AD_disk "CDTV_TEST")
(P_disk)


(copyfiles
  (help @copyfiles-help)
  (source ("%s:" #AD_disk))
  (dest #datadest)
  (all)
)


)

(
  (copyfiles
    (help @copyfiles-help)
    (source "SAVE.DAT")
    (dest #datadest)
  )

;----------------------------

(set #AD_disk "DGEN1")
(P_disk)

(copyfiles
  (help @copyfiles-help)
  (source ("%s:" #AD_disk))
  (dest #datadest)
  (all)
)

(set #AD_disk "DGEN2")
(P_disk)

(copyfiles
  (help @copyfiles-help)
  (source ("%s:" #AD_disk))
  (dest #datadest)
  (all)
)
)

)

(P_OpenDrawer)

(exit)
