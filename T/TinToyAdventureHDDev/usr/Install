;****************************

(set #sub-dir "data")		;sub directory containing data files
(set #readme-file "README")	;name of readme file
(set #cleanup "")		;files to delete after install
(set #prefix "TinToyAdventure")

;****************************
;----------------------------
; checks if given program is installed, if not abort install
; #program - to check

(procedure P_ChkRun
  (if
    (= 0 (run ("cd SYS:\nWhich %s" #program)))
    ("")
    (abort
      (cat
	"You must install \"" #program "\" first!\n"
	"It must be accessible via the path.\n"
	"You can find it in the WHDLoad package."
      )
    )
  )
)

(procedure P_ChkRunAminet
  (if
    (= 0 (run ("cd SYS:\nWhich %s" #program)))
    ("")
    (abort
      (cat
	"You must install \"" #program "\" first!\n"
	"It must be accessible via the path.\n"
	"You can find it on Aminet."
      )
    )
  )
)

;----------------------------
; Wait for inserting disk
; IN:  #AD_disk - name of disk
; OUT: -

(procedure P_disk

(until
  (or (exists ("DISK No.%s:" #diskno) (noreq)) (exists ("TinToyDisk%s:" #diskno) (noreq)))
  (message
    ("\nInsert Tin Toy Adventure disk %s in any drive!\n" #diskno)
    (all)
  )
)
(if (exists ("DISK No.%s" #diskno) (noreq))
(set #AD_disk ("DISK No.%s" #diskno))
(set #AD_disk ("TinToyDisk%s" #diskno))
)

  (askdisk
    (dest #AD_disk)
    (prompt ("\nInsert Disk \"%s\" in any drive!" #AD_disk))
    (help @askdisk-help)
  )
)

;****************************

(if
  (exists #readme-file)
  (if
    (= 0 (run ("SYS:Utilities/Multiview %s" #readme-file)))
    ("")
    (run ("SYS:Utilities/More %s" #readme-file))
  )
)

(set #program "WHDLoad")
(P_ChkRun)

(set @default-dest
  (askdir
    (prompt ("Where should \"%s\" be installed?\nA drawer \"%s\" will be automatically created." @app-name #prefix))
    (help @askdir-help)
    (default @default-dest)
    (disk)
  )
)
(set #dest (tackon @default-dest #prefix))
(if
  (exists #dest)
  (
    (set #choice
      (askbool
        (prompt ("\nDirectory \"%s\" already exists.\n Should it be deleted?" #dest))
        (default 1)
        (choices "Delete" "Skip")
        (help @askbool-help)
      )
    )
    (if
      (= #choice 1)
      (run ("Delete \"%s\" \"%s.info\" All" #dest #dest))
    )
  )
)
(makedir #dest
  (help @makedir-help)
  (infos)
)

;----------------------------

(copyfiles
  (help @copyfiles-help)
  (source ("%s.Slave" #prefix))
  (dest #dest)
)
(if
  (exists ("%s.newicon" #prefix))
  (set #icon
    (askchoice
      (prompt "\nWhich icon would you like to install?\n")
      (default 0)
      (choices "Normal" "NewIcon" "GlowIcon")
      (help @askchoice-help)
    )
  )
  (set #icon 0)
)
(select #icon
  (set #icon ("%s.inf" #prefix))
  (set #icon ("%s.newicon" #prefix))
  (set #icon ("%s.glowicon" #prefix))
)
(copyfiles
  (help @copyfiles-help)
  (source #icon)
  (newname ("%s.info" #prefix))
  (dest #dest)
)
(if
  (exists #readme-file)
  (copyfiles
    (help @copyfiles-help)
    (source #readme-file)
    (dest #dest)
  )
)
(if
  (exists ("%s.info" #readme-file))
  (copyfiles
    (help @copyfiles-help)
    (source ("%s.info" #readme-file))
    (dest #dest)
  )
)
(if
  (= #sub-dir "")
  ("")
  (
    (set #destparent #dest)
    (set #dest (tackon #dest #sub-dir))
    (makedir #dest
      (help @makedir-help)
    )
  )
)

;----------------------------

(set #diskno "1")
(P_disk)
(copyfiles
  (help @copyfiles-help)
  (source ("%s:" #AD_disk))
  (dest #dest)
  (pattern "(tt|TinToy)")
)
(copyfiles
  (help @copyfiles-help)
  (source ("%s:" #AD_disk))
  (dest #destparent)
  (pattern "(readme.doc)")
)
(if
  (exists (tackon #destparent "readme.doc"))
  (copyfiles
    (help @copyfiles-help)
    (source ("%s.info" #readme-file))
    (dest #destparent)
    (newname "readme.doc.info")
  )
)

(set #diskno "2")
(P_disk)
(copyfiles
  (help @copyfiles-help)
  (source ("%s:gamedata" #AD_disk))
  (dest #dest)
  (all)
)

;----------------------------

(run ("Delete %s ALL QUIET FORCE" #cleanup))

(exit)
