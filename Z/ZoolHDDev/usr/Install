;****************************

(set #readme-file "ReadMe")		;name of readme file
(set #docs-file "Manual")		;name of docs file
(set #highs-file "zool.high")	;name of high scores file
(set #cleanup "")			;files to delete after install
(set #last-file "Disk.2")		;last file the imager should create

;****************************
;----------------------------
; checks if given program is installed, if not abort install
; #program - to check

(procedure P_ChkRun
  (if
    (= 0 (run ("cd SYS:\nWhich %s" #program)))
    ("")
    (abort
      (cat
	"You must install \"" #program "\" first!\n"
	"It must be accessible via the path.\n"
	"You can find it in the WHDLoad package."
      )
    )
  )
)


(procedure P_RawImage
(working)
(run ("CD \"%s\"\nRawDIC SLAVE=%s.islave SOURCE=%s %s\nDelete %s.islave" #dest @app-name #CI_drive #ignore @app-name))

(if
  (exists (tackon #dest #last-file))
  ("")
  (abort "Diskimaging was not successful!\nRawDIC was unable to create all needed files!")
)
)
; ***********************

(procedure P_DicImage
  (message ("\nInsert %s disk 1 into drive %s !" @app-name #CI_drive))

  ;the following lines must be copied and adjusted for multiple disk images

  (set #CI_disknum 1)
  (set #CI_diskname ("%s Disk %ld" @app-name #CI_disknum))
  (set #CI_disklen 901120)
  (set #CI_skiptrk -1)
  (P_CreateImage)

  (message ("\nInsert %s disk 2 into drive %s !" @app-name #CI_drive))

  ;the following lines must be copied and adjusted for multiple disk images

  (set #CI_disknum 2)
  (set #CI_diskname ("%s Disk %ld" @app-name #CI_disknum))
  (P_CreateImage)

)

;----------------------------
; Create Image using DIC
; #dest        - path to save image in
; #CI_disknum  - number of the disk image to create
; #CI_diskname - label of the disk
; #CI_disklen  - length of disk image to create
; #CI_skiptrk  - track to skip, -1 means no skip
; #CI_drive    - drive to create image from

(procedure P_CreateImage
  (if
    (>= #CI_skiptrk 0)
    (set #option (cat " SKIPTRACK=" #CI_skiptrk))
    (set #option "")
  )
  (if
    (= 0
      (run 
        (cat
          "cd \"" #dest "\"\n"
          "DIC " #CI_drive " FD=" #CI_disknum " LD=" #CI_disknum " SIZE="
          #CI_disklen #option " PEDANTIC >CON:///1000//CLOSE"
        )
      )
    )
    (run ("FileNote \"%s.%ld\" \"%s\" Quiet" (tackon #dest "disk") #CI_disknum @app-name))
    (abort "\"DIC\" has failed to create a diskimage")
  )
)

; ***********************

(procedure P_CD32Copy

(set #datadest (tackon #dest "data"))
(makedir #datadest
  (help @makedir-help)
)

; for hiscore saves
(makedir (tackon #datadest "Zool.nvd")
  (help @makedir-help)
)

(copyfiles
  (help @copyfiles-help)
  (source "OSEmu.400")
  (dest #datadest)
)

(askdisk
	(prompt	("\n\n\n\n\n\nPlease insert %s CD-ROM in any drive" @app-name))
	(dest		"Zool")
	(help		"Insert the CD in your CD-ROM drive\nor you'll wait for a while!")
)

(copyfiles (source "Zool:")
           (dest #datadest)
	   (files)
	   (pattern "~(#?.xl|intro.bin|end.bin)")
)


)
;****************************

(if
  (exists #readme-file)
  (if
    (= 0 (run ("SYS:Utilities/Multiview %s" #readme-file)))
    ("")
    (run ("SYS:Utilities/More %s" #readme-file))
  )
)

(set #program "WHDLoad")
(P_ChkRun)


;----------------------------

(set #version
    (askchoice
      (prompt "\nWhich slave version would you like to install?\n")
      (default 0)
      (choices "AGA" "ECS" "AmigaFun" "CD³²")
      (help @askchoice-help)
    )
)

(if (> 2 #version)
(
(set #program "RawDIC")
(P_ChkRun)

(if
  (= @user-level 2)
  (
    (set #CI_drive
      (askchoice
	(prompt "Select source drive for diskimages")
	(default 0)
	(choices "DF0:" "DF1:" "DF2:" "DF3:")
	(help @askchoice-help)
      )
    )
    (select #CI_drive
      (set #CI_drive "DF0:")
      (set #CI_drive "DF1:")
      (set #CI_drive "DF2:")
      (set #CI_drive "DF3:")
    )
    (set #ignore " IGNOREERRORS")
  )
  (
  (set #CI_drive "DF0:")
  (set #ignore "")
  )
)
)
)

(if (= 2 #version)
(
(set #program "DIC")
(P_ChkRun)

(if
  (= @user-level 2)
  (
    (set #CI_drive
      (askchoice
	(prompt "Select source drive for diskimages")
	(default 0)
	(choices "DF0:" "DF1:" "DF2:" "DF3:" "Enter Device")
	(help @askchoice-help)
      )
    )
    (select #CI_drive
      (set #CI_drive "DF0:")
      (set #CI_drive "DF1:")
      (set #CI_drive "DF2:")
      (set #CI_drive "DF3:")
      (set #CI_drive
        (askstring
          (prompt "Select source drive for diskimages")
          (default "RAD:")
          (help @askstring-help)
        )
      )

    )
  )
  (set #CI_drive "DF0:")
)
)
)



(set default-dest
  (askdir
    (prompt ("Where should \"%s\" be installed?\nA drawer \"%s\" will be automatically created." @app-name @app-name))
    (help @askdir-help)
    (default @default-dest)
    (disk)
  )
)

(set @default-dest default-dest)



(set #dest (tackon @default-dest @app-name))
(if
  (exists #dest)
  (
    (set #choice
      (askbool
        (prompt ("\nDirectory \"%s\" already exists.\n Should it be deleted?" #dest))
        (default 1)
        (choices "Delete" "Skip")
        (help @askbool-help)
      )
    )
    (if
      (= #choice 1)
      (run ("Delete \"%s\" \"%s.info\" All" #dest #dest))
    )
  )
)
(makedir #dest
  (help @makedir-help)
  (infos)
)



;----------------------------

;(if
;  (exists ("%s.newicon" @app-name))
;  (set #icon
;    (askchoice
 ;     (prompt "\nWhich icon would you like to install?\nAll icons were created by Codetapper!\n")
 ;     (default 0)
 ;     (choices "Normal" "NewIcon" "GlowIcon")
 ;     (help @askchoice-help)
  ;  )
 ; )
 ; (set #icon 0)
;)
;(select #icon
;  (set #icon ("%s.inf" @app-name))
;  (set #icon ("%s.newicon" @app-name))
;  (set #icon ("%s.glowicon" @app-name))
  (set #icon ("%s.exoticon" @app-name))
;)
(copyfiles
  (help @copyfiles-help)
  (source #icon)
  (newname ("%s.info" @app-name))
  (dest #dest)
)
(tooltype
  (dest (tackon #dest @app-name))
  (settooltype "Slave" ("%s.slave" @app-name))
  (setdefaulttool "WHDLoad")
  (setstack 10240)
  (noposition)
)

(select #version
  (set #slave ("%sAGA.slave" @app-name))
  (set #slave ("%sECS.slave" @app-name))
  (set #slave ("%sAF.slave" @app-name))
  (set #slave ("%sCD32.slave" @app-name))
)

(copyfiles
  (help @copyfiles-help)
  (source #slave)
  (dest #dest)
  (newname ("%s.slave" @app-name))
)



(if
  (exists #readme-file)
  (copyfiles
    (help @copyfiles-help)
    (source #readme-file)
    (dest #dest)
  )
)
(if
  (exists ("%s.info" #readme-file))
  (
    (copyfiles
      (help @copyfiles-help)
      (source ("%s.info" #readme-file))
      (dest #dest)
    )
    (tooltype
      (dest (tackon #dest #readme-file))
      (noposition)
    )
  )
)
(if
  (exists #docs-file)
  (copyfiles
    (help @copyfiles-help)
    (source #docs-file)
    (dest #dest)
  )
)
(if
  (exists ("%s.info" #docs-file))
  (
    (copyfiles
      (help @copyfiles-help)
      (source ("%s.info" #docs-file))
      (dest #dest)
    )
    (tooltype
      (dest (tackon #dest #docs-file))
      (noposition)
    )
  )
)

(if (< 3 #version)

(if
  (exists #highs-file)
  (copyfiles
    (help @copyfiles-help)
    (source #highs-file)
    (dest #dest)
  )
)
)

(copyfiles
  (help @copyfiles-help)
  (source ("%s.islave" @app-name))
  (dest #dest)
)

(select #version
  (P_RawImage)	; aga
  (P_RawImage)	; ecs
  (P_DicImage)	; af
  (P_CD32Copy)  ; cd32
)


;----------------------------

(run ("Delete %s ALL QUIET FORCE" #cleanup))    ;delete temporary files

(exit)
