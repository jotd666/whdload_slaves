;****************************

(set #sub-dir "data")		;sub directory containing data files
(set #readme-file "readme")	;name of readme file
(set #manual-file "manual")	;name of manual file
(set #gamedir "BlockOut")      ; name of the game directory

;****************************

;----------------------------
; Checks if given program is reachable via the path
; if not abort install
; IN:  #program - to check
; OUT: -

(procedure P_chkrun
  (if
    (= 0 (run ("cd SYS:\nWhich %s" #program)))
    ("")
    (abort ("You must install \"%s\" first !\nIt must be accessible via the path.\nYou can find it in the whdload package." #program))
  )
)

;----------------------------
; Wait for inserting disk
; IN:  #AD_disk - name of disk
; OUT: -

(procedure P_disk
  (askdisk
    (dest #AD_disk)
    (prompt ("\nInsert Disk \"%s\" in any drive !" #AD_disk))
    (help @askdisk-help)
  )
)


;****************************

(if
  (exists #readme-file)
  (if 
    (= 0 (run ("SYS:Utilities/Multiview %s" #readme-file)))
    ("")
    (run ("SYS:Utilities/More %s" #readme-file))
  )
)

(set #program "WHDLoad")
(P_chkrun)

(set default-dest
  (askdir
    (prompt ("Where should \"%s\" installed ?\nA drawer \"%s\" will automatically created." @app-name #gamedir))
    (help @askdir-help)
    (default @default-dest)
    (disk)
  )
)
(set @default-dest (tackon default-dest #gamedir))
(if
  (exists @default-dest)
  (
    (set #choice
      (askbool
        (prompt ("\nDirectory \"%s\" already exists.\n Should it be deleted ?" @default-dest))
        (default 1)
        (choices "Delete" "Skip")
        (help @askbool-help)
      )
    )
    (if
      (= #choice 1)
      (run ("Delete \"%s\" \"%s.info\" All" @default-dest @default-dest))
    )
  )
)
(makedir @default-dest
  (help @makedir-help)
  (infos)
)

;----------------------------

(copyfiles
  (help @copyfiles-help)
  (source ("%s.slave" #gamedir))
  (dest @default-dest)
)

(if
  (exists ("%s.newicon" #gamedir))
  (set #icon
    (askchoice
      (prompt "\nWhich icon do you like to install ?\n")
      (default 0)
      (choices "Original icon" "Newicon" "Exoticon" "Chris Glowicon" "Frank Glowicon")
      (help @askchoice-help)
    )
  )
  (set #icon 0)
)
(select #icon
  (set #icon ("%s.inf" #gamedir))
  (set #icon ("%s.newicon" #gamedir))
  (set #icon ("%s.exoticon" #gamedir))
  (set #icon ("%s.chrisicon" #gamedir))
  (set #icon ("%s.frankglow" #gamedir))
)
(copyfiles
  (help @copyfiles-help)
  (source #icon)
  (newname ("%s.info" #gamedir))
  (dest @default-dest)
)
(if
  (exists #readme-file)
  (copyfiles
    (help @copyfiles-help)
    (source #readme-file)
    (dest @default-dest)
  )
)
(if
  (exists #manual-file)
  (copyfiles
    (help @copyfiles-help)
    (source #manual-file)
    (dest @default-dest)
  )
)
(if
  (exists ("%s.info" #manual-file))
  (copyfiles
    (help @copyfiles-help)
    (source ("%s.info" #manual-file))
    (dest @default-dest)
  )
)
(if
  (exists ("%s.info" #readme-file))
  (copyfiles
    (help @copyfiles-help)
    (source ("%s.info" #readme-file))
    (dest @default-dest)
  )
)
(if
  (= #sub-dir "")
  ("")
  (
    (set #dest (tackon @default-dest #sub-dir))
    (makedir #dest
      (help @makedir-help)
    )
  )
)

;----------------------------
(copyfiles
  (help @copyfiles-help)
  (source "blockout.set")
  (dest #dest)
)


(set #AD_disk "BLOCKOUT")
(P_disk)

(copyfiles
  (help @copyfiles-help)
  (source ("%s:" #AD_disk))
  (dest #dest)
  (files)
  (pattern "#?")
)

(exit)
